options {
  STATIC = false;
}


PARSER_BEGIN(RegFileParser)

package regparse;

class RegFileParser {

java.util.ArrayList<RegKey> parse() throws RegParseException {
    try {
        return registryKeyList();
    } catch (TokenMgrError e) {
        throw RegParseException.fromTokenMgrError(e);
    } catch (ParseException e) {
        throw RegParseException.fromParseException(e);
    } catch (RegTokenException e) {
        throw RegParseException.fromTokenException(e);
    }
}

}

PARSER_END(RegFileParser)


TOKEN : { < EOL : "\n" | "\r\n" > }
TOKEN : { < #NUMBER : ["0" - "9"] > }
TOKEN : { < HEADER : "Windows Registry Editor Version " (<NUMBER>)+ ( "." | <NUMBER> )* > }
TOKEN : { < #PRINTABLE : ( ["\u0020" - "\u007e"] | ["\u00a0" - "\ufffd"] ) > }
TOKEN : { < #PRINTABLE_NO_BACKSLASH : ( ["\u0020" - "\u005b"] | ["\u005d" - "\u007e"] | ["\u00a0" - "\ufffd"] ) > }
TOKEN : { < #HEX : ( ["0" - "9"] | ["a" - "f"] ) > }
TOKEN : { < #HEX_PAIR : ( (<HEX>){2} "," ) > }
TOKEN : { < #HEX_MULTILINE : (<HEX_PAIR>)* | ( (<HEX_PAIR>)+ "\\" <EOL> (" "){2} )* (<HEX_PAIR>)+ > }
TOKEN : { < #END_QUOTED : ( "\\" <PRINTABLE> | ~["\"", "\\", "\r", "\n"] )* "\"" > }
TOKEN : { < #QUOTED : "\"" <END_QUOTED> > }
TOKEN : { < #KEY_PART : ( <PRINTABLE_NO_BACKSLASH> )+ > }
TOKEN : { < KEY : "[" ( <KEY_PART> "\\" )* <KEY_PART> "]" > }
TOKEN : { < NAME : ( <QUOTED> | "@" ) "=" > }
TOKEN : { < TYPE : ( "\"" | "hex:" | "dword:" | "hex(b):" | "hex(7):" | "hex(2):" ) > }
TOKEN : { < VALUE_REG_SZ : <END_QUOTED> <EOL> > }
TOKEN : { < VALUE_REG_BINARY : <HEX_MULTILINE> (<HEX>){2} <EOL> > }
TOKEN : { < VALUE_REG_DWORD : (<HEX>){8} <EOL> > }
TOKEN : { < VALUE_REG_QWORD : (<HEX_PAIR>){7} <HEX> <EOL> > }
TOKEN : { < VALUE_REG_MULTI_SZ: <HEX_MULTILINE> "00" <EOL> > }
TOKEN : { < VALUE_REG_EXPAND_SZ : <HEX_MULTILINE> "00" <EOL> > }


java.util.ArrayList<RegKey> registryKeyList() :
{
    RegKey key;
    RegValue value;
}
{
    <HEADER><EOL>
    <EOL>
    { var keyList = new java.util.ArrayList<RegKey>(); }
    (
        key = registryKey()
        <EOL>
        (
            value = registryValue()
            { key.addValue(value); }
        )*
        <EOL>
        { keyList.add(key); }
    )*
    (<EOL>)*
    <EOF>
    { return keyList; }
}

RegKey registryKey():
{
    Token keyToken;
}
{
    keyToken = <KEY>
    { return RegKey.fromToken(keyToken); }
}

RegValue registryValue():
{
    Token nameToken;
    Token typeToken;
    Token valueToken;
}
{
    nameToken = <NAME>
    typeToken = <TYPE>
    (
        valueToken = <VALUE_REG_SZ>       |
        valueToken = <VALUE_REG_BINARY>   |
        valueToken = <EOL>                |
        valueToken = <VALUE_REG_DWORD>    |
        valueToken = <VALUE_REG_QWORD>    |
        valueToken = <VALUE_REG_MULTI_SZ> |
        valueToken = <VALUE_REG_EXPAND_SZ>
    )
    { return RegValue.fromTokens(nameToken, typeToken, valueToken); }
}
